package lyc.compiler;

import java.util.*;
import java_cup.runtime.*;
import lyc.compiler.files.AsmCodeGenerator;
import lyc.compiler.files.SymbolTableGenerator;
import lyc.compiler.files.IntermediateCodeGenerator;
import lyc.compiler.files.SymbolTableGenerator.Simbolo;

class Parser;

action code {:
  private Simbolo __simbolo;
  private boolean __logmsg = false;
  private boolean __logerrormsg = true;

  //Variable relacionada con ciclos while anidados
  private int __nloops = -1;

  //Variable relacionada con condiciones multiples
  private int __condmul = 0;

  private String __oplog = "";
  private String __opcmp = "";
  private String __opcmp_opuesto = "";

  //Variable relacionada con el bloque de declaracion
  private boolean __declaracion = false;

  //Variables relacionadas con la funcion figuales
  private int __cant_expr = 1;
  private String __figuales_pivot = "__figuales_pivot";
  private String __figuales_cont = "__figuales_cont";
  private String __figuales_exp = "__figuales_exp";

  //Variable relacionada con la funci√≥n Repeat Inline:
  private Integer __repeat_cantidad = 0;

  //Variables relacionadas con el bloque de declaracion
  private SymbolTableGenerator.Tipo __tipo_dato_var;
  private List<Object> __lista_variables = new ArrayList<Object>(); //lista que almacena temporalmente las variables declaradas de un mismo tipo, ej: a,b,c : Int, una vez que se pasa a tabla de simbolo se vacia
  private List<Object> __lista_declaracion = new ArrayList<Object>(); //lista que almacena todas las variables declaradas
:};

parser code {:
  //Override del metodo que reporta errores sintacticos
  public void syntax_error(Symbol cur_token){
    report_error("Error sintactico - ( Linea: " + (cur_token.left + 1) + " Columna: " + cur_token.right + " )\n",null);
    //TODO: Mirar porque esto no anda
    //System.out.println("----------" + cur_token.value); }
  }

  //https://www.cs.princeton.edu/~appel/modern/java/CUP/manual.html#errors
  public void report_fatal_error(String message, Object info) throws Exception{
    done_parsing();
    //TODO: Esto podria ser una CompilerException
    throw new Exception("Error sintactico, finalizando programa\n");
  }

  private SymbolTableGenerator.Tipo __tipo_expr_a = SymbolTableGenerator.Tipo.TIPO_X; //indica el tipo de dato de la expresion
  private SymbolTableGenerator.Tipo __tipo_expr_b = SymbolTableGenerator.Tipo.TIPO_X; //indica el tipo de dato de la expresion

  public void actualizar_tipo_expr(SymbolTableGenerator.Tipo nuevo_tipo){
      if(__tipo_expr_a == SymbolTableGenerator.Tipo.TIPO_X){
          __tipo_expr_a = nuevo_tipo;
      }
      else if(__tipo_expr_a == SymbolTableGenerator.Tipo.TIPO_STRING || nuevo_tipo == SymbolTableGenerator.Tipo.TIPO_STRING){
          System.out.println("No se puede utilizar una string en una expresion");
          System.exit(0);
      }
      else if(__tipo_expr_a == SymbolTableGenerator.Tipo.TIPO_INT && nuevo_tipo == SymbolTableGenerator.Tipo.TIPO_FLOAT){
          __tipo_expr_a = SymbolTableGenerator.Tipo.TIPO_FLOAT;
      }
      if(false){
        System.out.println("***********");
        System.out.println(__tipo_expr_a);
        System.out.println("***********");
      }
  }
:};

// Terminals
terminal T_ID;
terminal T_OR;
terminal T_AND;
terminal T_NOT;
terminal T_SUM;
terminal T_RES;
terminal T_MUL;
terminal T_DIV;
terminal T_ASIG;
terminal T_COMA;
terminal T_DOSP;
terminal T_PARA;
terminal T_PARC;
terminal T_CORA;
terminal T_CORC;
terminal T_LLAVA;
terminal T_LLAVC;
terminal T_MAYOR;
terminal T_MENOR;
terminal T_CINT;
terminal T_CFLOAT;
terminal T_CSTRING;
terminal T_PRIF;
terminal T_PRINT;
terminal T_PRELSE;
terminal T_PRINIT;
terminal T_PRREAD;
terminal T_PRWRITE;
terminal T_PRWHILE;
terminal T_PRFLOAT;
terminal T_PRSTRING;
terminal T_PRREPEAT;
terminal T_PRIGUALES;

// Non Terminals
non terminal codigo;
non terminal programa;
non terminal sentencia;
non terminal declaracion;
non terminal asignacion;
non terminal seleccion;
non terminal iteracion;
non terminal figuales;
non terminal frepeat;
non terminal entrada;
non terminal salida;

non terminal listavar;
non terminal listaexp;
non terminal decvar;
non terminal tipodato;
non terminal condicion;
non terminal contenido;
non terminal oplog;
non terminal opcomp;
non terminal expresion;
non terminal termino;
non terminal factor;

// Start Symbol Definition
start with programa;

programa ::=;
programa ::=
          {:
            if(__logerrormsg){
              if(!__declaracion){
                System.out.println("Programa sin declaracion previa\nFinalizando ejecucion");
                System.exit(0);
              }
            }
          :}

          codigo;

programa ::= declaracion
          {:
            if(__logerrormsg){
              if(!__declaracion){
                //System.out.println("Declaracion");
                __declaracion = true;
              }
              else{
                System.out.println("Declaracion previa\nFinalizando ejecucion");
                System.exit(0);
              }
            }
          :}

          codigo

          {:
            IntermediateCodeGenerator.insertar_etiquetas();
            new AsmCodeGenerator().traducir_codigo_intermedio();
          :};

codigo ::= sentencia;
codigo ::= codigo sentencia;

sentencia ::= asignacion;
sentencia ::= seleccion;
sentencia ::= iteracion;
sentencia ::= figuales;
sentencia ::= frepeat;
sentencia ::= entrada;
sentencia ::= salida;

declaracion ::= T_PRINIT T_LLAVA listavar T_LLAVC
              {:
                if(__logmsg){ System.out.println("bloque de declaracion"); }
              :};

asignacion ::= T_ID:id T_ASIG T_CSTRING:str
            {:
              __simbolo = SymbolTableGenerator.obtener_simbolo_de_tabla(id);
              if(__simbolo == null){
                //variable no declarada
                System.out.println("Variable `" + id + "` no declarada\nFinalizando ejecucion");
                System.exit(0);
              }
              if(__simbolo.tipo != SymbolTableGenerator.Tipo.TIPO_STRING){
                //no coincide el tipo de dato
                System.out.println("Asignacion de tipos incompatibles " + __simbolo.tipo + " " + SymbolTableGenerator.Tipo.TIPO_STRING + "\nFinalizando ejecucion");
                System.exit(0);
              }
              __simbolo = SymbolTableGenerator.almacenar_en_tabla(SymbolTableGenerator.Tipo.TIPO_STRING,str.toString());
              IntermediateCodeGenerator.insertar(__simbolo.nombre);
              IntermediateCodeGenerator.insertar("=");
              IntermediateCodeGenerator.insertar(id);
              if(__logmsg){ System.out.println("asignacion id - cstring"); }
            :};

asignacion ::= T_ID:id T_ASIG expresion
            {:
              __simbolo = SymbolTableGenerator.obtener_simbolo_de_tabla(id);
              if(__simbolo == null){
                //variable no declarada
                System.out.println("Variable `" + id + "` no declarada\nFinalizando ejecucion");
                System.exit(0);
              }
              if(__simbolo.tipo != __tipo_expr_a){
                  System.out.println("Asignacion de tipos incompatibles " + __simbolo.tipo + " " + __tipo_expr_a + "\nFinalizando ejecucion");
                  System.exit(0);
              }
              __tipo_expr_a = SymbolTableGenerator.Tipo.TIPO_X;
              IntermediateCodeGenerator.insertar("=");
              IntermediateCodeGenerator.insertar(id);
              if(__logmsg){ System.out.println("asignacion id - expresion"); }
            :};

seleccion ::= T_PRIF T_PARA condicion T_PARC T_LLAVA programa T_LLAVC
          {:
            if(__condmul > 0){
              --__condmul;
              IntermediateCodeGenerator.desapilar_e_insertar(0);
            }
            IntermediateCodeGenerator.desapilar_e_insertar(0);
            if(__logmsg){ System.out.println("seleccion"); }
          :};

seleccion ::= T_PRIF T_PARA condicion T_PARC T_LLAVA programa T_LLAVC
          {:
            IntermediateCodeGenerator.desapilar_e_insertar(2);
          :}

          T_PRELSE T_LLAVA

          {:
            IntermediateCodeGenerator.insertar("BI");
            IntermediateCodeGenerator.apilar();
          :}

          programa T_LLAVC

          {:
            IntermediateCodeGenerator.desapilar_e_insertar(1);
            if(__logmsg){ System.out.println("seleccion con else"); }
          :};

iteracion ::= T_PRWHILE
          {:
            IntermediateCodeGenerator.insertar("WHILE");
            IntermediateCodeGenerator.apilar();
            ++__nloops;
          :}

          T_PARA condicion T_PARC T_LLAVA programa T_LLAVC

          {:
            IntermediateCodeGenerator.desapilar_e_insertar(2);
            IntermediateCodeGenerator.insertar("BI");
            IntermediateCodeGenerator.insertar(IntermediateCodeGenerator.obtener_tope_de_pila() + __nloops);
            --__nloops;
            if(__logmsg){ System.out.println("iteracion"); }
          :};

figuales ::= T_PRIGUALES T_PARA expresion
          {:
            IntermediateCodeGenerator.insertar("=");
            IntermediateCodeGenerator.insertar(__figuales_pivot);
          :}

          T_COMA T_CORA listaexp T_CORC T_PARC

          {:
            while(__cant_expr > 1){
                IntermediateCodeGenerator.desapilar_e_insertar(0);
                --__cant_expr;
            }
            if(__logmsg){ System.out.println("#Iguales"); }
          :};

frepeat ::= T_PRREPEAT T_CINT:val
        {:
          IntermediateCodeGenerator.apilar();
          __repeat_cantidad = Integer.valueOf(val.toString());
        :}

        T_CORA

        codigo
        {:
          if(__repeat_cantidad > 0){
            //cuantas veces quiero repetir, y desde donde
            IntermediateCodeGenerator.repetir_codigo(__repeat_cantidad,IntermediateCodeGenerator.obtener_tope_de_pila());
          }
        :}

        T_CORC;

entrada ::= T_PRREAD T_PARA T_ID:id T_PARC
        {:
          if(SymbolTableGenerator.obtener_simbolo_de_tabla(id) == null){
            if(__logerrormsg){System.out.println("La variable " + id + " no esta declarada\nFinalizando programa"); }
            System.exit(0);
          }

          IntermediateCodeGenerator.insertar("READ");
          IntermediateCodeGenerator.insertar(id);
          //en AsmCodeGenerator cuando leo la etiqueta READ, marco como que es una asignacion
          //IntermediateCodeGenerator.insertar("=");
          if(__logmsg){ System.out.println("read"); }
        :};

salida ::= T_PRWRITE T_PARA T_ID:id T_PARC
        {:
          __simbolo = SymbolTableGenerator.obtener_simbolo_de_tabla(id);
          if(__simbolo == null){
            if(__logerrormsg){System.out.println("La variable " + id + " no esta declarada\nFinalizando programa"); }
            System.exit(0);
          }

          //TODO: Mirar si es necesario validar que la variable sea de tipo numerica o no
          IntermediateCodeGenerator.insertar("WRITE");
          IntermediateCodeGenerator.insertar(__simbolo.nombre);
          if(__logmsg){ System.out.println("write - id"); }
        :};

salida ::= T_PRWRITE T_PARA T_CSTRING:str T_PARC
        {:
          __simbolo = SymbolTableGenerator.almacenar_en_tabla(SymbolTableGenerator.Tipo.TIPO_STRING,str.toString());
          IntermediateCodeGenerator.insertar("WRITE");
          IntermediateCodeGenerator.insertar(__simbolo.nombre);
          if(__logmsg){ System.out.println("write - cstring"); }
        :};

//lista de variables
listavar ::= decvar;
listavar ::= listavar decvar;

//lista de expresiones
listaexp ::= expresion
          {:
            IntermediateCodeGenerator.insertar("=");
            IntermediateCodeGenerator.insertar(__figuales_exp);
            IntermediateCodeGenerator.insertar(__figuales_pivot);
            IntermediateCodeGenerator.insertar(__figuales_exp);
            IntermediateCodeGenerator.insertar("CMP");
            IntermediateCodeGenerator.insertar("BNE");
            IntermediateCodeGenerator.apilar_y_avanzar();
            IntermediateCodeGenerator.insertar("INC_+1");
            IntermediateCodeGenerator.insertar(__figuales_cont);
            IntermediateCodeGenerator.insertar("+");
            IntermediateCodeGenerator.insertar("=");
            IntermediateCodeGenerator.insertar(__figuales_cont);
            //IntermediateCodeGenerator.desapilar_e_insertar(0);
            ++__cant_expr;
          :};

listaexp ::= listaexp T_COMA expresion
          {:
            IntermediateCodeGenerator.insertar("=");
            IntermediateCodeGenerator.insertar(__figuales_exp);
            IntermediateCodeGenerator.insertar(__figuales_pivot);
            IntermediateCodeGenerator.insertar(__figuales_exp);
            IntermediateCodeGenerator.insertar("CMP");
            IntermediateCodeGenerator.insertar("BNE");
            IntermediateCodeGenerator.apilar_y_avanzar();
            IntermediateCodeGenerator.insertar("INC_+1");
            IntermediateCodeGenerator.insertar(__figuales_cont);
            IntermediateCodeGenerator.insertar("+");
            IntermediateCodeGenerator.insertar("=");
            IntermediateCodeGenerator.insertar(__figuales_cont);
            //IntermediateCodeGenerator.desapilar_e_insertar(0);
            ++__cant_expr;
          :};

//lista de sentencias
//listasent ::= sentencia;
//listasent ::= listasent T_COMA sentencia;

//declaracion de variables
decvar ::= T_ID:id
        {:
          if(__lista_declaracion.contains(id)){
              System.out.println("Variable '" + id + "' previamente declarada\nFinalizando programa");
              System.exit(0);
          }
          __lista_variables.add(id);
          SymbolTableGenerator.almacenar_en_tabla(SymbolTableGenerator.Tipo.TIPO_ID,id.toString());
        :}

        T_COMA decvar;

decvar ::= T_ID:id
        {:
          if(__lista_declaracion.contains(id)){
              System.out.println("Variable '" + id + "' previamente declarada\nFinalizando programa");
              System.exit(0);
          }
          __lista_variables.add(id);
          SymbolTableGenerator.almacenar_en_tabla(SymbolTableGenerator.Tipo.TIPO_ID,id.toString());
        :}

        T_DOSP tipodato

        {:
          SymbolTableGenerator.actualizar_tipo_de_dato(__lista_variables,__tipo_dato_var);
          __lista_declaracion.addAll(__lista_variables);
          __lista_variables.clear();
        :};

tipodato ::= T_PRINT
          {:
            __tipo_dato_var = SymbolTableGenerator.Tipo.TIPO_INT;
            if(__logmsg){ System.out.println("t_print"); }
          :};

tipodato ::= T_PRFLOAT
          {:
            __tipo_dato_var = SymbolTableGenerator.Tipo.TIPO_FLOAT;
            if(__logmsg){ System.out.println("t_prfloat"); }
          :};

tipodato ::= T_PRSTRING
          {:
            __tipo_dato_var = SymbolTableGenerator.Tipo.TIPO_STRING;
            if(__logmsg){ System.out.println("t_prstring"); }
          :};

contenido ::= expresion
          {:
            __tipo_expr_b = __tipo_expr_a;
            __tipo_expr_a = SymbolTableGenerator.Tipo.TIPO_X;
          :}

          opcomp expresion

          {:
            if(__tipo_expr_a != __tipo_expr_b){
              System.out.println("Comparacion de tipos no compatibles " + __tipo_expr_b + " " + __tipo_expr_a + "\nFinalizando programa");
              System.exit(0);
            }
            __tipo_expr_a = SymbolTableGenerator.Tipo.TIPO_X;
            __tipo_expr_b = SymbolTableGenerator.Tipo.TIPO_X;
          :};

condicion ::= contenido
          {:
            IntermediateCodeGenerator.insertar("CMP");
            IntermediateCodeGenerator.insertar(__opcmp_opuesto);
            IntermediateCodeGenerator.apilar_y_avanzar();
            if(__logmsg){ System.out.println("condicion simple"); }
          :};

condicion ::= T_NOT contenido
          {:
            IntermediateCodeGenerator.insertar("NOT");
            IntermediateCodeGenerator.insertar("CMP");
            IntermediateCodeGenerator.insertar(__opcmp);
            IntermediateCodeGenerator.apilar_y_avanzar();
            if(__logmsg){ System.out.println("condicion negada"); }
          :};

condicion ::= contenido oplog
          {:
            IntermediateCodeGenerator.insertar("CMP");
            if(__oplog.equals("OR")){
                IntermediateCodeGenerator.insertar(__opcmp);
            }
            else{
                IntermediateCodeGenerator.insertar(__opcmp_opuesto);
            }
            IntermediateCodeGenerator.apilar_y_avanzar();
            //IntermediateCodeGenerator.insertar(__oplog);
          :}

          contenido

          {:
            IntermediateCodeGenerator.insertar("CMP");
            IntermediateCodeGenerator.insertar(__opcmp_opuesto);
            if(__oplog.equals("OR")){
                IntermediateCodeGenerator.desapilar_e_insertar(1);
            }
            else{
                ++__condmul;
            }
            IntermediateCodeGenerator.apilar_y_avanzar();
            if(__logmsg){ System.out.println("condicion multiple"); }
          :};

oplog ::= T_OR
        {:
          __oplog = "OR";
          if(__logmsg){ System.out.println("or"); }
        :};

oplog ::= T_AND
        {:
          __oplog = "AND";
          if(__logmsg){ System.out.println("and"); }
        :};

opcomp ::= T_MAYOR
        {:
          __opcmp = "BGT";
          __opcmp_opuesto = "BLE";
          if(__logmsg){ System.out.println("mayor"); }
        :};

opcomp ::= T_MENOR
        {:
          __opcmp = "BLT";
          __opcmp_opuesto = "BGE";
          if(__logmsg){ System.out.println("menor"); }
        :};

expresion ::= termino
          {:
            if(__logmsg){ System.out.println("expresion = termino"); }
          :};

expresion ::= expresion T_SUM termino
          {:
            IntermediateCodeGenerator.insertar("+");
            if(__logmsg){ System.out.println("+"); }
          :};

expresion ::= expresion T_RES termino
          {:
            IntermediateCodeGenerator.insertar("-");
            if(__logmsg){ System.out.println("-"); }
          :};

termino ::= factor
        {:
          if(__logmsg){ System.out.println("termino = factor"); }
        :};

termino ::= termino T_MUL factor
        {:
          IntermediateCodeGenerator.insertar("*");
          if(__logmsg){ System.out.println("*"); }
        :};

termino ::= termino T_DIV:val factor
        {:
          IntermediateCodeGenerator.insertar("/");
          if(__logmsg){ System.out.println("/"); }
        :};

factor ::= T_ID:val
        {:
          __simbolo = SymbolTableGenerator.obtener_simbolo_de_tabla(val);
          if(__simbolo == null){
               System.out.println("La variable " + val + " no fue declarada\nFinalizando ejecucion");
               System.exit(0);
          }
          IntermediateCodeGenerator.insertar(val);
          actualizar_tipo_expr(__simbolo.tipo);
          if(__logmsg){ System.out.println("factor = id ( id: " + val + " )"); }
        :};

factor ::= T_CINT:val
        {:
          __simbolo = SymbolTableGenerator.almacenar_en_tabla(SymbolTableGenerator.Tipo.TIPO_INT,val.toString());
          IntermediateCodeGenerator.insertar(__simbolo.nombre);
          actualizar_tipo_expr(__simbolo.tipo);
          if(__logmsg){ System.out.println("factor = cint ( cint: " + val + " )"); }
        :};

factor ::= T_CFLOAT:val
        {:
          __simbolo = SymbolTableGenerator.almacenar_en_tabla(SymbolTableGenerator.Tipo.TIPO_FLOAT,val.toString());
          IntermediateCodeGenerator.insertar(__simbolo.nombre);
          actualizar_tipo_expr(__simbolo.tipo);
          if(__logmsg){ System.out.println("factor = cfloat ( cfloat: " + val + " )"); }
        :};

factor ::= T_PARA expresion T_PARC
        {:
          if(__logmsg){ System.out.println("factor = ( expresion )"); }
        :};